# shortcut to this dotfiles path is $ZSHDOT
export ZSHDOT=$HOME/.dotfiles
export DOTFILE=$ZSHDOT

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSHDOT/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# oh-my-zsh configuration
ZSH=$HOME/.oh-my-zsh
ZSH_THEME="bullet-train"

plugins=(git colorize vagrant brew osx colored-man docker mvn npm sbt urltools)

source $ZSH/oh-my-zsh.sh

# Source the dotfiles system aliases to fix conflict with oh-my-zshes ls
source $ZSHDOT/system/aliases.zsh

unset config_files
